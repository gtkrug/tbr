buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }

//        gradle versions plugin
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "com.github.erdi:webdriver-binaries-gradle-plugin:3.2"
        classpath "org.grails.plugins:hibernate5:8.1.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:4.4.0"
        classpath "org.grails.plugins:database-migration:4.2.0"

//        gradle versions plugin
        classpath "com.github.ben-manes:gradle-versions-plugin:0.51.0"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.18'
}

version "$tbrVersion"
group "tm.binding.registry"

apply plugin:"idea"
apply plugin:"war"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"org.grails.grails-web"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"

// gradle versions plugin
apply plugin: "com.github.ben-manes.versions"

// ICL Artifactory Metadata
ext {
    artifactoryBaseUrl = 'https://artifactory.icl.gtri.org:443/artifactory/'
    artifactoryTrustmarkMavenReleaseRepoUrl = "$artifactoryBaseUrl" + "trustmark-maven"
    artifactoryExternalReleaseRepoUrl = "$artifactoryBaseUrl" + "external"
    artifactoryProxiedReleaseRepoUrl = "$artifactoryBaseUrl" + "proxied"
}

def iclArtifactoryUsername = System.getenv('ICL_ARTIFACTORY_USERNAME')
if (!iclArtifactoryUsername?.trim()) {
    iclArtifactoryUsername = project.properties['artifactoryUsername']
}

def iclArtifactoryPassword = System.getenv('ICL_ARTIFACTORY_PASSWORD')
if (!iclArtifactoryPassword?.trim()) {
    iclArtifactoryPassword = project.properties['artifactoryPassword']
}


repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }

    maven {
        name 'proxied'
        url artifactoryProxiedReleaseRepoUrl
        credentials {
            username iclArtifactoryUsername
            password iclArtifactoryPassword
        }
    }
    maven {
        name 'central'
        url artifactoryTrustmarkMavenReleaseRepoUrl

        credentials {
            username = iclArtifactoryUsername
            password = iclArtifactoryPassword
        }
    }
    maven {
        name 'icl-central'
        url artifactoryExternalReleaseRepoUrl

        credentials {
            username = iclArtifactoryUsername
            password = iclArtifactoryPassword
        }
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

// This creates the folder 'migrations' where all database schema changes are stored
sourceSets {
    main {
        resources {
            srcDir "grails-app/migrations"
        }
    }
}
dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation "org.springframework.boot:spring-boot-starter-logging"

    implementation "org.springframework.boot:spring-boot-starter-validation"

    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"

    implementation( "org.grails:grails-core" ) {
        exclude group: 'org.fusesource.jansi', module: 'jansi:1.18'
    }
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.1'

    implementation "org.grails:grails-dependencies"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"

    implementation "org.grails:grails-plugin-rest"

    console "org.grails:grails-console"

    implementation "org.grails.plugins:cache:5.0.1"
    implementation "org.grails.plugins:gsp"
    implementation "org.grails.plugins:scaffolding"

    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.6.15.Final"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation "org.springframework.security:spring-security-taglibs:5.8.12"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.grails:grails-web-jsp'
    implementation 'javax.servlet:jstl:1.2'
    implementation 'taglibs:standard:1.1.2'

    runtimeOnly "javax.servlet.jsp:javax.servlet.jsp-api:2.3.3"

    implementation "edu.gatech.gtri.trustmark:tf-grails-shared-views:0.9.5"

    implementation "org.hibernate:hibernate-ehcache:5.6.15.Final"
    implementation 'edu.gatech.gtri.trustmark:tf-api-impl:1.4.74'
    implementation 'edu.gatech.gtri.trustmark:tf-api:1.4.46'

    implementation 'org.grails.plugins:quartz:2.0.13' // For monitoring configuration directories periodically
    implementation 'org.quartz-scheduler:quartz:2.2.3'

    implementation 'org.dom4j:dom4j:2.1.4'

    runtimeOnly ('org.grails.plugins:database-migration:4.2.1') {
        // It seems that this module conflicts with the Tomcat boot process
        exclude group: 'org.springframework.boot', module: 'spring-boot-cli'
        exclude group: 'com.vaadin.external.google', module:'android-json'
    }

    implementation('org.liquibase:liquibase-core:4.20.0') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation 'org.yaml:snakeyaml:1.33'

    implementation 'com.github.blagerweij:liquibase-sessionlock:1.6.9'

    profile "org.grails.profiles:web:5.0.5"

    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc:10.1.23"

    implementation 'mysql:mysql-connector-java:8.0.33'

    implementation 'jaxen:jaxen:2.0.0'

    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:4.3.0"

    testImplementation "org.grails.plugins:geb:4.0.0"
    testImplementation "org.grails:grails-gorm-testing-support:3.2.2"
    testImplementation "org.grails:grails-web-testing-support:3.2.2"

    implementation "org.seleniumhq.selenium:selenium-htmlunit-driver:2.52.0"
    implementation 'net.sourceforge.htmlunit:htmlunit:2.70.0'
}

tasks.withType(War).configureEach { War war ->
    war.dependsOn compileGroovyPages
}

war {
    enabled=true
    baseName = "tbr"
}

bootWar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    enabled = true
    dependsOn compileGroovyPages
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

webdriverBinaries {
    chromedriver '2.45.0'
    geckodriver '0.30.0'
}

war {
    baseName = "tm-binding-registry"
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

assets {
    minifyJs = false
    minifyCss = false
}

// copied from: http://nimavat.me/blog/grails3-add-custom-build-info-to-war
buildProperties {
    inputs.property("info.app.buildDate", new Date().format('yyyy-MM-dd HH:mm:ss'))
}

apply plugin: "maven-publish"

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "edu.gatech.gtri.trustmark"
            artifactId = "tbr"
            version = "$tbrVersion"

            from components.web
        }
    }

    repositories {
        maven {
            name 'central'
            url artifactoryTrustmarkMavenReleaseRepoUrl

            credentials {
                username = iclArtifactoryUsername
                password = iclArtifactoryPassword
            }
        }
    }
}

assets {
    packagePlugin = true
}